###################################################################################
# Document Properties Section

SET DOCUMENT Name = "Unit Test #16"
SET DOCUMENT Description = "Equivalencing based on term ordering (#97, #109)"
SET DOCUMENT Version = "1.0"
SET DOCUMENT Copyright = "Copyright (c) 2012, Selventa. All rights reserved."
SET DOCUMENT ContactInfo = "support@belframework.org"
SET DOCUMENT Authors = "Selventa, Inc."
SET DOCUMENT Licenses = "Creative Commons Attribution-Non-Commercial-ShareAlike 3.0 Unported License"

##################################################################################
# Definitions Section

DEFINE NAMESPACE PFH AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-human-protein-families.belns"
DEFINE NAMESPACE NCH AS URL "http://resource.belframework.org/belframework/1.0/namespace/selventa-named-human-complexes.belns"
DEFINE NAMESPACE HGNC AS URL "http://resource.belframework.org/belframework/1.0/namespace/hgnc-approved-symbols.belns"
DEFINE NAMESPACE CHEBI AS URL "http://resource.belframework.org/belframework/1.0/namespace/chebi-names.belns"
DEFINE NAMESPACE GO AS URL "http://resource.belframework.org/belframework/1.0/namespace/go-biological-processes-names.belns"
DEFINE NAMESPACE EGID AS URL "http://resource.belframework.org/belframework/1.0/namespace/entrez-gene-ids-hmr.belns"
DEFINE NAMESPACE CHEBIID AS URL "http://resource.belframework.org/belframework/1.0/namespace/chebi-ids.belns"

DEFINE ANNOTATION CellLine AS  URL "http://resource.belframework.org/belframework/1.0/annotation/atcc-cell-line.belanno"
DEFINE ANNOTATION Species AS  URL "http://resource.belframework.org/belframework/1.0/annotation/species-taxonomy-id.belanno"

##################################################################################
# Statements Section

SET Evidence = "Testing order in complexes."
complex(p(HGNC:JUN),p(HGNC:FOS))
complex(p(HGNC:FOS),p(HGNC:JUN))

SET Evidence = "Testing resolution of named complex with composed complex. Note that current resource documents do not expand the human AP-1 complex, so components are only enumerated here."
complex(NCH:"AP 1 Complex") hasComponents list(p(HGNC:FOS),p(HGNC:JUN))

SET Evidence = "Testing order in reaction reactant lists, 2 statements should have equivalent object terms"
cat(p(HGNC:NOS3)) -> rxn(reactants(a(CHEBI:dioxygen),a(CHEBI:"L-arginine")),products(a(CHEBI:"nitric oxide")))
cat(p(PFH:"NOS Family")) -> rxn(reactants(a(CHEBI:"L-arginine"), a(CHEBI:dioxygen)),products(a(CHEBI:"nitric oxide")))

SET Evidence = "Testing order in reaction product lists, 2 statements should have equivalent object terms.CHEBIID 17650 is equivalent to cortisol"
cat(p(HGNC:HSD11B1)) => rxn(reactants(a(CHEBI:NADPH), a(CHEBI:cortisone)), products(a(CHEBI:"NADP(+)"),a(CHEBI:cortisol)))
cat(p(HGNC:HSD11B1)) => rxn(reactants(a(CHEBI:NADPH), a(CHEBI:cortisone)), products(a(CHEBIID:17650),a(CHEBI:"NADP(+)")))

SET Evidence = "Testing order in composites; 2 statements should have equivalent subject terms. EGID 1950 is equivalent to EGF"
composite(a(CHEBI:progesterone),p(HGNC:EGF)) -> r(HGNC:FOS)
composite(p(EGID:1950),a(CHEBI:progesterone)) -> tscript(p(PFH:"STAT5 Family"))
